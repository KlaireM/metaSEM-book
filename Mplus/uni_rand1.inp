TITLE:  Random-effects model: Single level approach
DATA:   FILE IS Jaramillo05.dat;	
VARIABLE: NAMES id r r_v IDV OC_alpha JP_alpha;
        USEVARIABLES ARE r w2;  ! Use both r and w2 in the analysis
        MISSING ARE *;

DEFINE: w2 = SQRT(r_v**(-1));   ! Weight for transformation           
        r = w2*r;               ! Transformed r

ANALYSIS: TYPE=RANDOM;          ! Use random slopes analysis
          ESTIMATOR=ML;         ! Use ML estimation

MODEL:	
        [r@0.0];                ! Intercept fixed at 0
        r@1.0;                  ! Error variance fixed at 1
        f | r ON w2;            ! f: Study specific random effects
        f*;                     ! var(f): tau^2
        [f*];                   ! mean(f): Average effect size beta_R

OUTPUT: SAMPSTAT;
        CINTERVAL(symmetric);   ! Wald CI
